import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId 'com.yaerin.xposed.hide'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getMyVersionCode()
        versionName getMyVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.google.code.gson:gson:2.8.0'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'de.robv.android.xposed:api:82:sources'
}

static def getMyVersionCode() {
    return Integer.parseInt(new SimpleDateFormat("yyMMdd").format(new Date()))+4
}

static def getMyVersionName() {
    return "1.3.2." + "git describe --always".execute().getText().trim()
}
